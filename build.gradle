/*
*	Kryptnostic conductor client
* 	version is defined in kryptnostic-gradles/kryptnostic.gradle
*/

description = "Kryptnostic Conductor Client"
group = "com.kryptnostic"
ext.projectName = "kryptnostic-conductor-client"

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "jacoco"

apply from: "http://api.krypt.int/gradle/kryptnostic.gradle"

sourceCompatibility=JavaVersion.VERSION_1_8

dependencies { 
    /*
     * SL4J
     */
    compile group: "org.slf4j", name: "slf4j-api", version:"${slf4j_version}"
  
    /*
     * @Inject and @Nullable support
     */
    compile "javax.inject:javax.inject:${javax_inject_version}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_version}"
    compile 'com.esotericsoftware:kryo:4.0.0'
    compile 'com.esotericsoftware:minlog:1.3.0'
    compile 'com.esotericsoftware:reflectasm:1.11.3'
	/*
     * Kryptnostic dependencies
     */
    if( project.hasProperty('developmentMode') && project.developmentMode ) {
        logger.quiet(project.name + " using project dependencies.")
        compile project(":kryptnostic-mapstores")
    } else {
        logger.quiet(project.name + " using jar dependencies.")
        compile "com.kryptnostic:kryptnostic-mapstores:${mapstores_version}"
    }

    
    /*
     * TESTING
     */
    testCompile "junit:junit:${junit_version}"
    testCompile( "org.mockito:mockito-all:${mockito_version}" ) {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from sourceSets.test.output
}

configurations {
    tests
      published.extendsFrom tests, archives 
}

artifacts {
	archives sourcesJar, javadocJar
	tests testJar
}

install { 
	configuration = configurations.published 
} 

eclipse {
    ext.downloadSources = true
    ext.downloadJavadoc = true
    ext.sourceCompatibility=JavaVersion.VERSION_1_8
}

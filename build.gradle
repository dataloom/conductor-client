plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jacoco"
    id 'checkstyle'
    id 'findbugs'
}

description = "Kryptnostic Conductor Client"
group = "com.kryptnostic"

ext.projectName = "kryptnostic-conductor-client"
ext.scmUrl = 'scm:git:https://github.com/dataloom/conductor-client.git'
ext.connectionUrl = 'scm:git:https://github.com/dataloom/conductor-client.git'
ext.developerConnectionUrl = 'scm:git:https://github.com/dataloom/conductor-client.git'

apply from: "https://raw.githubusercontent.com/kryptnostic/gradles/master/kryptnostic.gradle"

sourceCompatibility=JavaVersion.VERSION_1_8

def versions = [
  checkstyle: '7.4',
]

checkstyle {
  toolVersion = "${versions.checkstyle}"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies { 
    /*
     * SL4J
     */
    compile group: "org.slf4j", name: "slf4j-api", version:"${slf4j_version}"
  
    /*
     * @Inject and @Nullable support
     */
    compile "javax.inject:javax.inject:${javax_inject_version}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_version}"
    compile 'com.esotericsoftware:kryo-shaded:4.0.0'
    compile 'com.esotericsoftware:minlog:1.3.0'
    compile 'com.esotericsoftware:reflectasm:1.11.3'
    compile 'org.objenesis:objenesis:2.4'

    
    compile "com.google.guava:guava:${guava_version}"
    compile "org.apache.olingo:odata-commons-api:${odata_version}"
    compile "org.apache.olingo:odata-commons-core:${odata_version}"
    compile "org.apache.olingo:odata-server-api:${odata_version}"
    compile "org.apache.olingo:odata-server-core:${odata_version}"

    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: "${cassandra_driver_version}"
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-extras', version: "${cassandra_driver_version}"
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-mapping', version: "${cassandra_driver_version}"
    compile "org.xerial.snappy:snappy-java:${snappy_version}"
    compile "net.jpountz.lz4:lz4:${lz4_version}"

	/*
     * Kryptnostic dependencies
     */
    if( project.hasProperty('developmentMode') && project.developmentMode ) {
        logger.quiet(project.name + " using project dependencies.")
        compile project(":loom-api")
        compile project(":rhizome")
        testCompile( project(path: ":loom-api", configuration: "tests"))
        testCompile( project(path: ":rhizome", configuration: "tests"))
        testCompile( project(path: ":rhizome-client", configuration: "tests"))
    } else {
        logger.quiet(project.name + " using jar dependencies.")
        compile "com.kryptnostic:loom-api:${loom_api_version}"
        compile "com.kryptnostic:rhizome:${rhizome_version}"
        //testCompile "com.kryptnostic:rhizome:${rhizome_version}:tests"
        testCompile "com.kryptnostic:loom-api:${loom_api_version}:tests"
        testCompile "com.kryptnostic:rhizome:${rhizome_version}:tests"
        testCompile "com.kryptnostic:rhizome-client:${rhizome_client_version}:tests"
    }

    /*
     * TESTING
     */
    testCompile "org.cassandraunit:cassandra-unit:3.1.1.0"
    testCompile "junit:junit:${junit_version}"
    testCompile( "org.mockito:mockito-all:${mockito_version}" ) {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
}

install {
	configuration = configurations.published
}

eclipse {
    ext.downloadSources = true
    ext.downloadJavadoc = true
    ext.sourceCompatibility=JavaVersion.VERSION_1_8
}
